extends Node2D

@onready var health_ui := $UI/HealthUI
@onready var score_ui := $UI/ScoreUI
@onready var rewind_ui := $UI/RewindUI
@onready var high_score_game_over := $UI/HighScore
@onready var player := $Player
@onready var rewind_cooldown_timer := $Player/RewindCooldownTimer
@onready var background := $Background
var rewind_cooldown_percentage := 1.0

 # INFO: Variable for the lives the player has remaining, with an accompanying set function which updates the UI
var lives := 3:
	set(value):
		lives = value
		if health_ui:
			health_ui.init_lives(lives)
@export var default_lives := 3

 # INFO: Variable for score

var score := 0:
	set(value):
		score = value
		score_ui.score = score

 # INFO: Function run only once when the game starts running
func _ready():
	lives = default_lives
	score = 0
	high_score_game_over.visible = false
	# Spawn wave 1
	Enemy._current_wave = 1
	var melee_scene = load("res://Scenes/melee_enemy.tscn")
	for i in Enemy.WAVE_INITIAL_ENEMY_COUNT:
		var enemy = melee_scene.instantiate()
		enemy.position = Vector2(randf_range(Enemy.WAVE_SPAWN_X_MIN, Enemy.WAVE_SPAWN_X_MAX), -80.0)
		_connect_enemy_signals(enemy)
		$Enemies.add_child(enemy)


func _connect_enemy_signals(enemy: Enemy) -> void:
	enemy.enemy_killed.connect(_on_enemy_killed)
	enemy.tree_exited.connect(_on_enemy_removed)


func _spawn_wave() -> void:
	var canvas_transform = get_canvas_transform()
	var screen_top_y = -canvas_transform.origin.y
	var spawn_y = screen_top_y + Enemy.WAVE_SPAWN_Y_OFFSET

	var enemy_count = mini(Enemy.WAVE_INITIAL_ENEMY_COUNT + Enemy._current_wave - 1, Enemy.WAVE_MAX_ENEMY_COUNT)

	var melee_scene = load("res://Scenes/melee_enemy.tscn")
	var ranged_scene = load("res://Scenes/ranged_enemy.tscn")

	for i in enemy_count:
		var enemy_scene: PackedScene
		if Enemy._current_wave <= 2:
			enemy_scene = melee_scene
		else:
			enemy_scene = melee_scene if randf() > 0.5 else ranged_scene

		var enemy = enemy_scene.instantiate()
		var spawn_x = randf_range(Enemy.WAVE_SPAWN_X_MIN, Enemy.WAVE_SPAWN_X_MAX)
		enemy.position = Vector2(spawn_x, spawn_y + randf_range(-30.0, 30.0))
		_connect_enemy_signals(enemy)
		$Enemies.call_deferred("add_child", enemy)

	if Enemy._current_wave % Enemy.WAVE_MINI_BOSS_EVERY == 0:
		var boss_scene = load("res://Scenes/mini_boss.tscn")
		var boss = boss_scene.instantiate()
		boss.position = Vector2((Enemy.WAVE_SPAWN_X_MIN + Enemy.WAVE_SPAWN_X_MAX) / 2.0, spawn_y - 50.0)
		_connect_enemy_signals(boss)
		$Enemies.call_deferred("add_child", boss)


func _on_enemy_killed(score_value):
	score = score + score_value


func _on_enemy_removed() -> void:
	for e in get_tree().get_nodes_in_group("enemies"):
		if e is Enemy and e.is_inside_tree():
			return
	Enemy._current_wave += 1
	_spawn_wave()

func game_over() -> void:
	high_score_game_over.visible = true

func _physics_process(delta: float) -> void:
	rewind_ui.fill_rewind_bar(player.rewind_data_length()/2)
	rewind_cooldown_percentage = (1 - rewind_cooldown_timer.time_left/rewind_cooldown_timer.wait_time)
	if rewind_cooldown_percentage != 1:
		rewind_ui.modulate = Color(1.0, 1.0, 1.0, rewind_cooldown_percentage/2)
	else:
		rewind_ui.modulate = Color(1.0, 1.0, 1.0, 1.0)

func pause_enemies(pause:=true) -> void:
	for i in get_tree().get_nodes_in_group("pausable"):
		i.pause(pause)
